# Maintainer: Jeffrey Feng <galago1992@gmail.com>

pkgbase=poppler
pkgname=('poppler' 'poppler-glib')
pkgver=0.85.0
pkgrel=1
arch=(x86_64)
license=('GPL')
makedepends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg2' 'gtk3' 'pkgconfig' 'lcms2'
             'icu' 'git' 'nss' 'curl' 'poppler-data'
             'cmake' 'python' 'boost')
options=('!emptydirs')
url="https://github.com/jonathanffon/poppler-lcd-patch"
_commit=6518e62aabc3dd98add63af1bb7198699042304d
source=("https://poppler.freedesktop.org/poppler-${pkgver}.tar.xz"
        "git+https://github.com/jonathanffon/poppler-lcd-patch#commit=${_commit}")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd poppler-${pkgver}

  patch -p1 < ../poppler-lcd-patch/poppler-glib-enable-subpixel.patch
  patch -p1 < ../poppler-lcd-patch/poppler-render-subpixel-by-cairo.patch

  cd ../build
  cmake ../poppler-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_UNSTABLE_API_ABI_HEADERS=ON \
    -DENABLE_GTK_DOC=OFF \
    -DBUILD_CPP_TESTS=OFF \
    -DBUILD_GTK_TESTS=OFF \
    -DBUILD_QT5_TESTS=OFF \
    -DENABLE_GOBJECT_INTROSPECTION=OFF \
    -DENABLE_QT5=OFF \
    -DENABLE_QT_STRICT_ITERATORS=OFF \
    -DRUN_GPERF_IF_PRESENT=OFF \
    -DWITH_GObjectIntrospection=OFF \
    -DENABLE_ZLIB_UNCOMPRESS=OFF
  make
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0 with subpixel patch on LCD display"
  depends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg2' 'lcms2' 'nss' 'curl')
  optdepends=('poppler-data: encoding data to display PDF documents containing CJK characters')
  conflicts=('poppler' "poppler-qt3<${pkgver}" "poppler-qt4<${pkgver}")
  provides=("poppler=${pkgver}")

  cd build
  make DESTDIR="${pkgdir}" install

  # cleanup for splitted build
  rm -vrf "${pkgdir}"/usr/include/poppler/{glib,qt5}
  rm -vf "${pkgdir}"//usr/lib/libpoppler-{glib,qt5}.*
  rm -vf "${pkgdir}"/usr/lib/pkgconfig/poppler-{glib,qt5}.pc
  rm -vrf "${pkgdir}"/usr/{lib,share}/gir*
  rm -vrf "${pkgdir}"/usr/share/gtk-doc
}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings with subpixel patch on LCD display"
  depends=("poppler=${pkgver}" 'glib2')
  conflicts=('poppler-glib')
  provides=('poppler-glib')

  cd build

  make -C glib DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-glib.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -vf "${pkgdir}"/usr/lib/libpoppler.*
  rm -vf "${pkgdir}/usr/bin/poppler-glib-demo"
}
